マップのキーと値を入れ替える
============================

マップのキーと値を入れ替えるには、タプルのリストにして、要素を入れ替えた上でマップに戻します。
タプルの要素を入れ替えるには`Data.Tuple`の`swap`を使います。

```haskell
> import Data.Tuple (swap)
> xs = Map.fromList [(5,"a"), (3,"b")]
> Map.fromList $ map swap $ Map.toList xs
fromList [("a",5),("b",3)]
```

この方法では重複がある場合、後にある方が優先されます。

```haskell
> ys = Map.fromList [(5,"a"), (3,"b"), (7,"a")]
> Map.fromList $ map swap $ Map.toList ys
fromList [("a",7),("b",3)]
```


`Set`を使って重複する値を扱うには次のようにします。
`Set`は`containers`パッケージに含まれる、集合を表すコレクションです。

```haskell
> import Control.Arrow (second)
> import qualified Data.Set as Set
> Map.fromListWith Set.union $ map (second Set.singleton . swap) $ Map.toList ys
fromList [("a",fromList [5,7]),("b",fromList [3])]
```

ここで`Control.Arrow`の`second`を使っています。
この関数は、二要素タプルのふたつ目の要素に関数を適用する関数です。

```haskell
> second (* 5) ("a", 10)
("a",50)
> second odd ("a", 10)
("a",False)
> second Set.singleton ("a", 10)
("a",fromList [10])
```
