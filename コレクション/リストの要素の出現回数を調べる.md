リストの要素の出現回数を調べる
==============================

どの要素が何回出現するかを調べるには、いくつかの関数を組み合わせることで対応できます。

`sort`と`group`は`Data.List`に定義されている関数です。

```haskell
import Data.List

counts :: Ord a => [a] -> [(a, Int)]
counts = map count . group . sort
  where count xs = (head xs, length xs)
```

使ってみましょう。

```haskell
> counts ["pen", "pinapple", "apple", "pen"]
[("apple",1),("pen",2),("pinapple",1)]
> counts ["zun", "zun", "zun", "doko"]
[("doko",1),("zun",3)]
```

解説
----

`group`は連続する同じ要素をリストにまとめます。

連続していないとまとめられないので、最初に`sort`で並び替えておきます。

これらの関数を使うと以下のように処理できます。

```haskell
> import Data.List
> sort ["pen", "pinapple", "apple", "pen"]
["apple","pen","pen","pinapple"]
> group $ sort ["pen", "pinapple", "apple", "pen"]
[["apple"],["pen","pen"],["pinapple"]]
```

同じ要素のみからなるリストのリストができました。

次に個々のリストに対し、先頭の要素と長さを取得します。
先頭の要素は`head`、長さは`length`で取得できます。

```haskell
> head ["pen", "pen"]
"pen"
> length ["pen", "pen"]
2
```

`head`と`length`をタプルにして返す関数を作ります。

```haskell
> count xs = (head xs, length xs)
> count ["pen", "pen"]
("pen",2)
```

先のリストにこの関数を`map`で適用すれば完成です。

```haskell
> map count $ group $ sort ["pen", "pinapple", "apple", "pen"]
[("apple",1),("pen",2),("pinapple",1)]
```
